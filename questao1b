// limpa variável
operation clear(A){
 1: if zero A then goto 0 else goto 2
 2: do dec A goto 1
}

// A:=A+B (soma destrutiva)
operation soma(A,B){
  1: if zero B then goto 0 else goto 2
  2: do dec B goto 3
  3: do inc A goto 1
}

// A:=A+B (soma não-destrutiva)
operation soma(A,B,C){
  1: if zero B then goto 5 else goto 2
  2: do dec B goto 3
  3: do inc A goto 4
  4: do inc C goto 1
  5: if zero C then goto 0 else goto 6
  6: do dec C goto 7
  7: do inc B goto 5
}

// A:=B (atribuição destrutiva)
operation load(A,B){
  1: do clear(A) goto 2
  2: do soma(A,B) goto 0
}

// A:=B usando C (atribuição não-destrutiva)
operation load(A,B,C){
  1: do clear(A) goto 2
  2: do soma(A,B,C) goto 0
}

operation mult(A,B,C,D,E){ //A := A*B, nao destrutivo
  1: do load(C,A,D) goto 2
  2: do load(E,B,D) goto 3
  3: do dec E goto 4
  4: if zero E then goto 7 else goto 5
  5: do dec E goto 6
  6: do soma(A,C,D) goto 4
  7: do clear(C) goto 0
}

// Programa principal, f(x) = x^3+x
main {
   1: do load(A,X,C) goto 2
   2: do mult(A,X,B,C,D) goto 3
   3: do mult(A,X,B,C,D) goto 4
   4: do soma(A,X,C) goto 5
   5: do load(Y,A) goto 0
}
